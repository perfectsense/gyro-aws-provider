
plugins {
    id 'java'
    id 'maven-publish'
    id 'checkstyle'
}

version = '0.14-SNAPSHOT'
sourceCompatibility = 1.8

def aws_sdk2_version = '2.5.8'

defaultTasks 'build', 'check'

if (System.getenv("TRAVIS_BRANCH") == null) {
    check.dependsOn 'publishToMavenLocal'
} else {
    check.dependsOn 'publish'
}

configurations {
    gyroDoclet
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://artifactory.psdops.com/public"
    }
    maven {
        url "https://artifactory.psdops.com/gyro-snapshots"
    }
}

dependencies {
    compile "software.amazon.awssdk:apache-client:${aws_sdk2_version}"
    compile "software.amazon.awssdk:ec2:${aws_sdk2_version}"
    compile "software.amazon.awssdk:s3:${aws_sdk2_version}"
    compile "software.amazon.awssdk:iam:${aws_sdk2_version}"
    compile "software.amazon.awssdk:sqs:${aws_sdk2_version}"
    compile "software.amazon.awssdk:elasticloadbalancing:${aws_sdk2_version}"
    compile "software.amazon.awssdk:elasticloadbalancingv2:${aws_sdk2_version}"
    compile "software.amazon.awssdk:autoscaling:${aws_sdk2_version}"
    compile "software.amazon.awssdk:cloudfront:${aws_sdk2_version}"
    compile "software.amazon.awssdk:cloudwatch:${aws_sdk2_version}"
    compile "software.amazon.awssdk:route53:${aws_sdk2_version}"
    compile "software.amazon.awssdk:kms:${aws_sdk2_version}"
    compile "software.amazon.awssdk:rds:${aws_sdk2_version}"
    compile "software.amazon.awssdk:sns:${aws_sdk2_version}"
    compile "software.amazon.awssdk:cognitoidentityprovider:${aws_sdk2_version}"
    compile "software.amazon.awssdk:dlm:${aws_sdk2_version}"
    compile "software.amazon.awssdk:waf:${aws_sdk2_version}"
    compile "software.amazon.awssdk:lambda:${aws_sdk2_version}"

    compile 'com.psddev:dari-util:3.3.607-xe0f27a'
    compile "com.google.guava:guava:23.0"
    compile "gyro:gyro-core:0.14-SNAPSHOT"

    checkstyle 'com.puppycrawl.tools:checkstyle:8.15'

    gyroDoclet "gyro:gyro-doclet:0.14-SNAPSHOT"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'gyro'
            artifactId = 'gyro-aws-provider'
            version = version

            from components.java
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv("GRADLE_DEPLOY_USER")
                password System.getenv("GRADLE_DEPLOY_PASSWORD")
            }

            if (project.version.endsWith('-SNAPSHOT')) {
                url "https://artifactory.psdops.com/gyro-snapshots"
            } else {
                url "https://artifactory.psdops.com/gyro-releases"
            }
        }
    }
}

task referenceDocs(type: Javadoc) {
    title = null // Prevents -doctitle and -windowtitle from being passed to BeamDoclet
    source = sourceSets.main.allJava
    classpath = configurations.compile
    options.doclet = "gyro.doclet.GyroDoclet"
    options.docletpath = configurations.gyroDoclet.files.asType(List)
}

checkstyle {
    toolVersion = '6.13'
    configFile rootProject.file('config/checkstyle.xml')
    checkstyleMain.enabled = false
    checkstyleTest.enabled = false
}

task checkstyle(type: Checkstyle) {
    configFile rootProject.file('config/checkstyle.xml')
    source 'src'
    include '**/*.java'
    exclude '**/generated-src/**'

    classpath = files()
}
