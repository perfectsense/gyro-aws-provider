/*
 * Copyright 2019, Perfect Sense, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

def releaseVersion = '0.99.1'
def releaseBuild = false;

if (System.getenv('TRAVIS_BRANCH') && System.getenv('TRAVIS_PULL_REQUEST') == 'false') {
    defaultTasks 'build', 'publish'

    releaseBuild = System.getenv('TRAVIS_BRANCH').startsWith("release/");
} else {
    defaultTasks 'build', 'publishToMavenLocal'
}

group = 'gyro'
version = releaseBuild ? releaseVersion : releaseVersion + '-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url 'https://artifactory.psdops.com/public'
    }

    maven {
        url 'https://artifactory.psdops.com/gyro-snapshots'
    }
}

configurations {
    gyroDoclet
}

shadowJar {
    classifier = ''
    include '*.jar'
    include 'com/fasterxml/**'
    include 'com/google/**'
    include 'org/json/**'
    include 'gyro/aws/**'

    // Relocating common dependencies
    relocate 'com.fasterxml', 'shaded.gyro.aws.fasterxml'
    relocate 'com.google', 'shaded.gyro.aws.google'
    relocate 'org.json', 'shaded.gyro.aws.json'
}

build.dependsOn shadowJar

dependencies {
    api 'gyro:gyro-core:0.99.1-SNAPSHOT'

    implementation 'com.google.guava:guava:23.0'
    implementation 'com.psddev:dari-util:3.3.607-xe0f27a'
    implementation enforcedPlatform('software.amazon.awssdk:bom:2.13.1')
    implementation 'software.amazon.awssdk:apache-client'
    implementation 'software.amazon.awssdk:autoscaling'
    implementation 'software.amazon.awssdk:cloudfront'
    implementation 'software.amazon.awssdk:cloudwatch'
    implementation 'software.amazon.awssdk:cloudwatchevents'
    implementation 'software.amazon.awssdk:cognitoidentityprovider'
    implementation 'software.amazon.awssdk:dlm'
    implementation 'software.amazon.awssdk:ec2'
    implementation 'software.amazon.awssdk:elasticloadbalancing'
    implementation 'software.amazon.awssdk:elasticloadbalancingv2'
    implementation 'software.amazon.awssdk:elasticsearch'
    implementation 'software.amazon.awssdk:iam'
    implementation 'software.amazon.awssdk:kms'
    implementation 'software.amazon.awssdk:lambda'
    implementation 'software.amazon.awssdk:rds'
    implementation 'software.amazon.awssdk:route53'
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:sns'
    implementation 'software.amazon.awssdk:sqs'
    implementation 'software.amazon.awssdk:waf'
    implementation 'software.amazon.awssdk:elasticache'
    implementation 'software.amazon.awssdk:sts'
    implementation 'software.amazon.awssdk:docdb'
    implementation 'software.amazon.awssdk:acm'
    implementation 'software.amazon.awssdk:acmpca'
    implementation 'software.amazon.awssdk:dynamodb'
    implementation 'software.amazon.awssdk:neptune'
    implementation 'org.json:json:20180813'

    gyroDoclet "gyro:gyro-doclet:0.99.0-SNAPSHOT"
}

task referenceDocs(type: Javadoc) {
    title = null // Prevents -doctitle and -windowtitle from being passed to GyroDoclet
    source = sourceSets.main.allJava
    classpath = configurations.runtimeClasspath
    options.doclet = "gyro.doclet.GyroDoclet"
    options.docletpath = configurations.gyroDoclet.files.asType(List)
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('GRADLE_DEPLOY_USER')
                password System.getenv('GRADLE_DEPLOY_PASSWORD')
            }

            if (releaseBuild) {
                url 'https://artifactory.psdops.com/gyro-releases'

            } else {
                url 'https://artifactory.psdops.com/gyro-snapshots'
            }
        }
    }
}
